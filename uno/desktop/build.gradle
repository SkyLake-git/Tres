apply plugin: "java-library"
sourceCompatibility = 1.8
sourceSets.main.java.srcDirs = ["src/"]
sourceSets.main.resources.srcDirs = ["../assets"]
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
project.ext.mainClassName = "tres.DesktopLauncher"
project.ext.assetsDir = new File("../assets")

jar.manifest.attributes = ['Main-Class': project.ext.mainClassName]

repositories {
    mavenLocal()
    mavenCentral()
    google()
    gradlePluginPortal()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    maven { url "https://jitpack.io" }
}
ext {
    appName = "Tres"
    gdxVersion = '1.11.0'
    roboVMVersion = '2.3.16'
    box2DLightsVersion = '1.5'
    ashleyVersion = '1.7.4'
    aiVersion = '1.8.2'
    gdxControllersVersion = '2.2.1'
}

sourceSets {
    main {
        output.resourcesDir = java.outputDir
    }
}

import groovy.json.JsonBuilder
import org.gradle.internal.os.OperatingSystem

import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

task run(dependsOn: classes, type: JavaExec) {
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true

    if (OperatingSystem.current() == OperatingSystem.MAC_OS) {
        // Required to run on macOS
        jvmArgs += "-XstartOnFirstThread"
    }
}

task debug(dependsOn: classes, type: JavaExec) {
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true
    debug = true
}

task dist(type: Jar) {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    manifest {
        attributes 'Main-Class': project.mainClassName
    }
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

task createResource {
    doLast {
        // ビルド時刻
        def now = LocalDateTime.now()
        // git リビジョン
        String rev = "git rev-parse HEAD".execute().text.trim()

        // Json文字列を生成
        def jsonBuilder = new JsonBuilder()
        jsonBuilder(
                built_at: now.format(DateTimeFormatter.ofPattern("yyyy-MM-dd HH:mm:ss")),
                git_revision: rev)

        String json = jsonBuilder.toPrettyString()

        String base = sourceSets.main.resources.srcDirs[0] as String

        File file = new File(base + "/build_info.json")
        // リソースファイルとして保存
        file.getParentFile().mkdirs()
        file.createNewFile()
        file.write(json)
    }
}

// ビルド時に自動で実行されるように、依存関係の設定。
processResources.dependsOn createResource


dist.dependsOn classes

dependencies {
    implementation project(":uno:core")
    implementation 'com.github.jafarlihi:eemit:0.1.0'
    // implementation 'org.riversun:java-promise:1.1.0'
    implementation 'com.auth0:java-jwt:4.2.1'
    implementation 'com.crashinvaders.vfx:gdx-vfx-core:0.5.1'
    implementation 'com.crashinvaders.vfx:gdx-vfx-effects:0.5.1'    // Optional, if you need standard filter/effects.
    api "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    api "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
    api "com.badlogicgames.gdx:gdx-bullet-platform:$gdxVersion:natives-desktop"
    api "com.badlogicgames.gdx-controllers:gdx-controllers-desktop:$gdxControllersVersion"
    api "com.badlogicgames.gdx:gdx-freetype-platform:1.11.0:natives-desktop"
    api "com.badlogicgames.gdx:gdx:$gdxVersion"
    api "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
    api "com.badlogicgames.box2dlights:box2dlights:$box2DLightsVersion"
    api "com.github.tommyettinger:jbump:1.0.1"
    api "com.kotcrab.vis:vis-ui:1.4.11"
    api "com.badlogicgames.ashley:ashley:$ashleyVersion"
    api "com.badlogicgames.gdx:gdx-bullet:$gdxVersion"
    api "com.badlogicgames.gdx:gdx-ai:$aiVersion"
    api "com.badlogicgames.gdx-controllers:gdx-controllers-core:$gdxControllersVersion"
    api "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    implementation 'ch.qos.logback:logback-classic:1.2.9'
    implementation 'org.jetbrains:annotations:23.0.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.7.1'
}
